package App::KSP_CKAN::Roles::Logger;

use Log::Log4perl;
use Method::Signatures;
use Moo::Role;

# ABSTRACT: Logging for KSP_CKAN

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

Provides a central logging service for KSP_CKAN

=head1 DESCRIPTION

  with('App::KSP_CKAN::Roles::Logger');

Can be consumed by any KSP_CKAN package. Config is required.

Inspiratation/Credit here -> http://stackoverflow.com/questions/3018528/making-self-logging-modules-with-loglog4perl

=cut

requires 'config';

my @methods = qw(
  log trace debug info warn error fatal
  is_trace is_debug is_info is_warn is_error is_fatal
  logexit logwarn error_warn logdie error_die
  logcarp logcluck logcroak logconfess
);

has _logger => (
  is        => 'ro',
  isa       => sub { 'Log::Log4perl::Logger' },
  lazy      => 1,
  builder   => 1,
  handles   => \@methods,
);

has _log_level  => ( is => 'ro', lazy => 1, builder => 1 );
has log_config  => ( is => 'ro', lazy => 1, builder => 1 );

method _build_log_config() {
  # TODO: Integrate MooX::Options
  my $log_level = $self->_log_level;
  my $log_path = $self->config->working;
  return qq(
    log4perl.rootLogger              = $log_level
    log4perl.appender.SCREEN         = Log::Log4perl::Appender::Screen
    log4perl.appender.SCREEN.stderr  = 0
    log4perl.appender.SCREEN.layout  = Log::Log4perl::Layout::PatternLayout
    log4perl.appender.SCREEN.layout.ConversionPattern = %m %n
    log4perl.appender.LOG1           = Log::Log4perl::Appender::File
    log4perl.appender.LOG1.utf8      = 1
    log4perl.appender.LOG1.filename  = $log_path/KSP_CKAN.log
    log4perl.appender.LOG1.mode      = append
    log4perl.appender.LOG1.layout    = Log::Log4perl::Layout::PatternLayout
    log4perl.appender.LOG1.layout.ConversionPattern = %d %p %m %n
  );
}

method _build__log_level {
  if ( ! $self->config->debugging ) {
    return "INFO, LOG1";
  } else {
    return "DEBUG, LOG1, SCREEN";
  }
}

around $_ => sub {
  my $orig = shift;
  my $this = shift;

  # one level for this method itself
  # two levels for Class:;MOP::Method::Wrapped (the "around" wrapper)
  # one level for Moose::Meta::Method::Delegation (the "handles" wrapper)
  local $Log::Log4perl::caller_depth;
  $Log::Log4perl::caller_depth += 4;

  my $return = $this->$orig(@_);

  $Log::Log4perl::caller_depth -= 4;
  return $return;

} foreach @methods;

method _build__logger() {
  my $this = shift;

  my $loggerName = ref($this);
  $self->log_config;
  Log::Log4perl->init_once(\$self->log_config);
  return Log::Log4perl->get_logger($loggerName)
}

1;
